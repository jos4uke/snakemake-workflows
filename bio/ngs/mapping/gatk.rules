"""
GATK based postprocessing of BAM files. Indels can be realigned and base qualities
can be recalibrated.

For usage, include this in your workflow.

Expects a global variable CONFIG (e.g. parsed from json) of at least the
following structure, assuming that the desired reference sequence is some genome
to be found under the given path:

{
    "references": {
        "genome": "path/to/genome.fasta"
    },
    "dbsnp": "path/to/dbsnp.vcf"
}
"""


__author__ = "Johannes KÃ¶ster"
__license__ = "MIT"


rule gatk_realign_info:
    input:
        ref=_get_ref,
        bam="mapping/{reference}/{prefix}.bam"
    output:
        temp("mapping/{reference}/{prefix}.realign.intervals")
    shell:
        "gatk -T RealignerTargetCreator -R {input.ref} "
        "-I {input.bam} -known {CONFIG[dbsnp]} "
        "-o {output}"


rule gatk_realign_bam:
    input:
        ref=_get_ref,
        bam="mapping/{reference}/{prefix}.bam",
        intervals="mapping/{reference}/{prefix}.realign.intervals"
    output:
        "mapping/{reference}/{prefix}.realigned.bam"
    shell:
        "gatk -T IndelRealigner -R {input.ref} "
        "-I {input.bam} -targetIntervals {input.intervals} "
        "-o {output}"


rule gatk_recalibrate_info:
    input:
        ref=_get_ref,
        bam="mapping/{reference}/{prefix}.bam"
    output:
        temp("mapping/{reference}/{prefix}.recalibrate.grp")
    shell:
        "gatk -T BaseRecalibrator -R {input.ref} "
        "-I {input.bam} -knownSites {CONFIG[dbsnp]} "
        "-o {output}"


rule gatk_recalibrate_bam:
    input:
        ref=_get_ref,
        bam="mapping/{reference}/{prefix}.bam",
        grp="mapping/{reference}/{prefix}.recalibrate.grp"
    output:
        "mapping/{reference}/{prefix}.recalibrated.bam"
    shell:
        "gatk -T PrintReads -R {input.ref} "
        "-I {input.bam} -BQSR {input.grp} "
        "-o {output}"


def _get_ref(wildcards):
    return CONFIG["references"][wildcards.reference]
